{
  config,
  lib,
  pkgs,
  ...
}:

let
  cfg = config.programs.kitty-extended;
  stylixCfg = config.programs.stylix-extended;

  # Parse a base16 YAML scheme file to extract the palette
  parseScheme =
    schemeFile:
    let
      jsonFile = pkgs.runCommand "scheme.json" { nativeBuildInputs = [ pkgs.yq-go ]; } ''
        yq -o=json '.palette' ${schemeFile} > $out
      '';
    in
    builtins.fromJSON (builtins.readFile jsonFile);

  # Generate a Kitty theme configuration from a base16 palette
  # Reference: https://sw.kovidgoyal.net/kitty/conf/#color-scheme
  generateKittyTheme = palette: ''
    # Base16 theme generated by Stylix
    foreground ${palette.base05}
    background ${palette.base00}
    selection_foreground ${palette.base00}
    selection_background ${palette.base05}
    cursor ${palette.base05}
    cursor_text_color ${palette.base00}

    # Normal colors (0-7)
    color0 ${palette.base00}
    color1 ${palette.base08}
    color2 ${palette.base0B}
    color3 ${palette.base0A}
    color4 ${palette.base0D}
    color5 ${palette.base0E}
    color6 ${palette.base0C}
    color7 ${palette.base05}

    # Bright colors (8-15)
    color8 ${palette.base03}
    color9 ${palette.base08}
    color10 ${palette.base0B}
    color11 ${palette.base0A}
    color12 ${palette.base0D}
    color13 ${palette.base0E}
    color14 ${palette.base0C}
    color15 ${palette.base07}

    # Extended colors
    url_color ${palette.base0D}
    active_tab_foreground ${palette.base05}
    active_tab_background ${palette.base02}
    inactive_tab_foreground ${palette.base04}
    inactive_tab_background ${palette.base01}
  '';

  # Resolve scheme paths from stylix-extended config
  darkSchemeFile = "${pkgs.base16-schemes}/share/themes/${stylixCfg.colorSchemes.dark}.yaml";
  lightSchemeFile = "${pkgs.base16-schemes}/share/themes/${stylixCfg.colorSchemes.light}.yaml";

  # Parse palettes
  darkPalette = parseScheme darkSchemeFile;
  lightPalette = parseScheme lightSchemeFile;

in
{
  options.programs.kitty-extended = {
    enable = lib.mkEnableOption "Kitty terminal with Stylix theme integration" // {
      default = true;
    };

    generateStylixThemes = lib.mkOption {
      type = lib.types.bool;
      default = true;
      description = "Generate Kitty theme files from Stylix color schemes";
    };

    shell = lib.mkOption {
      type = lib.types.nullOr lib.types.str;
      default = "${pkgs.zellij}/bin/zellij";
      description = "Shell command to run in kitty";
    };

    opacity = lib.mkOption {
      type = lib.types.float;
      default = 0.8;
      description = "Background opacity (0.0-1.0)";
    };
  };

  config = lib.mkIf cfg.enable {
    home.packages = [ pkgs.kitty-themes ];

    programs.kitty = {
      enable = true;

      font = lib.mkIf stylixCfg.enable {
        inherit (config.stylix.fonts.monospace) name;
        size = stylixCfg.sizes.terminal;
      };

      settings = {
        shell = lib.mkIf (cfg.shell != null) cfg.shell;
        shell_integration = "no-title";
        disable_ligatures = "cursor";
        allow_hyperlinks = true;

        confirm_os_window_close = 0;
        window_padding_width = 0;
        sync_to_monitor = true;
        draw_minimal_borders = true;
        remember_window_position = true;
        strip_trailing_spaces = "always";

        pointer_shape_when_dragging = "beam crosshair";
        background_opacity = cfg.opacity;
        background_blur = 1;
        dynamic_background_opacity = true;

        wayland_titlebar_color = "background";

        cursor_trail = 10;
        cursor_trail_decay = "0.1 0.4";
        cursor_trail_start_threshold = 2;
        cursor_shape = "block";
        cursor_blink_interval = "0.5";

        bell_on_tab = "ðŸ”” ";
        enable_audio_bell = true;
        window_alert_on_bell = true;
        visual_bell_duration = "2.0 ease-in linear";
        bell_border_color = "#f38ba8";
        notify_on_cmd_finish = ''invisible 10.0 command notify-send "job finished with status: %s" %c'';

        tab_bar_edge = "top";
        tab_bar_style = "powerline";
        tab_powerline_style = "slanted";
        tab_bar_background = "none";

        url_style = "curly";
        open_url_with = "default";
        detect_urls = true;

        mouse_hide_wait = "3.0";
        copy_on_select = false;

        allow_remote_control = "socket-only";
        listen_on = "unix:@kitty";
      };

      keybindings = {
        "ctrl+shift+e" = "open_url_with_hints";
        "f11" = "toggle_fullscreen";
      };
    };

    # Generate Stylix-based theme files for darkman to switch between
    xdg.configFile = lib.mkIf cfg.generateStylixThemes {
      "kitty/themes/stylix-dark.conf".text = generateKittyTheme darkPalette;
      "kitty/themes/stylix-light.conf".text = generateKittyTheme lightPalette;
    };

    xdg.desktopEntries.kitty = {
      name = "kitty";
      genericName = "Terminal";
      exec = "kitty";
      icon = "utilities-terminal";
      comment = "Fast, feature-rich, GPU based terminal";
      categories = [
        "System"
        "TerminalEmulator"
      ];
      terminal = false;
    };
  };
}
